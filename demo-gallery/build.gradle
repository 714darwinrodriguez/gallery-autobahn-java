apply plugin: getPlugin()

String PLUGIN_ANDROID = "com.android.application"

String getPlugin() {
    String buildPlugin = project.properties.get("buildPlatform")
    switch (buildPlugin) {
        case "netty":
            return "application"
        default:
            return "com.android.application"
    }
}

if (plugins.hasPlugin(PLUGIN_ANDROID)) {
    android {
        compileSdkVersion 26
        buildToolsVersion "26.0.0"

        packagingOptions {
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/ASL2.0'
            exclude 'META-INF/INDEX.LIST'
            exclude 'META-INF/io.netty.versions.properties'
        }

        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 26
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        return true
    }

    dependencies {
        implementation project(path: ':autobahn')
        implementation 'com.android.support:appcompat-v7:26.0.0-alpha1'
    }
} else {
    mainClassName = "io.crossbar.autobahn.demogallery.netty.Main"
    dependencies {
        implementation project(path: ':autobahn')
    }
    sourceSets {
        main {
            java {
                include 'io/crossbar/autobahn/demogallery/netty/**'
            }
        }
    }
}
